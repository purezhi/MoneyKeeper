ext.deps = [:]
def versions = [:]
// gradle_plugin
versions.gradle_plugin = "3.0.1"
// test
versions.junit = "4.12"
versions.test_runner = "1.0.1"
versions.espresso = "3.0.1"
// 支持库
versions.support = "27.0.2"
// ViewModel and LiveData
versions.lifecycle = "1.0.0"
// Room
versions.room = "1.0.0"
// rxjava2
versions.rxjava2 = "2.1.3"
versions.rxandroid = "2.0.1"
// BaseRecyclerViewAdapterHelper
versions.BRVAH = "2.9.34"
versions.floo = "1.1.0"
versions.datetimepicker = "3.5.1"
versions.MPChart = "v3.0.3"
versions.leakcanary = "1.5.4"

def deps = [:]

deps.gradle_plugin = "com.android.tools.build:gradle:$versions.gradle_plugin"

deps.junit = "junit:junit:$versions.junit"
deps.test_runner = "com.android.support.test:runner:$versions.test_runner"
deps.spresso_core = "com.android.support.test.espresso:espresso-core:$versions.espresso"

def support = [:]
support.app_compat = "com.android.support:appcompat-v7:$versions.support"
support.design = "com.android.support:design:$versions.support"
support.cardview = "com.android.support:cardview-v7:$versions.support"
deps.support = support

def lifecycle = [:]
lifecycle.extensions = "android.arch.lifecycle:extensions:$versions.lifecycle"
lifecycle.compiler = "android.arch.lifecycle:compiler:$versions.lifecycle"
deps.lifecycle = lifecycle

def room = [:]
room.runtime = "android.arch.persistence.room:runtime:$versions.room"
room.rxjava2 = "android.arch.persistence.room:rxjava2:$versions.room"
room.compiler = "android.arch.persistence.room:compiler:$versions.room"
deps.room = room

deps.rxjava = "io.reactivex.rxjava2:rxjava:$versions.rxjava2"
deps.rxandroid = "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"

deps.BRVAH = "com.github.CymChad:BaseRecyclerViewAdapterHelper:$versions.BRVAH"
deps.floo = "me.drakeet.floo:floo:$versions.floo"
deps.datetimepicker = "com.wdullaer:materialdatetimepicker:$versions.datetimepicker"
deps.MPChart = "com.github.PhilJay:MPAndroidChart:$versions.MPChart"
deps.leakcanary_debug = "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary"
deps.leakcanary_release = "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakcanary"

ext.deps = deps

def build_versions = [:]
build_versions.compile_sdk = 27
build_versions.min_sdk = 21
build_versions.target_sdk = 27
ext.build_versions = build_versions

def static addRepos(RepositoryHandler handler) {
    handler.google()
    handler.jcenter()
    handler.maven {
        url "https://jitpack.io"
    }
}

ext.addRepos = this.&addRepos

def static uniformSupportVersion(ConfigurationContainer configurations) {
    // 统一支持库使用 27.0.2 版本
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '27.0.2'
                }
            }

        }
    }
}

ext.uniformSupportVersion = this.&uniformSupportVersion